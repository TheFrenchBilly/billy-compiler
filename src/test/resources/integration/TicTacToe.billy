// Tic tac toe in billy 0.0.1
String choice

// Array will have been good ...
String[] grid

String state // D -> Draw, X -> X win, O -> O win 
String player // X or O
Integer nbTurn // The number of turn played

main()
	ticTacToe()

ticTacToe()
	reset()
	menu()
	if choice == "1":
		play()
		ticTacToe()
	elseif choice != "2":
		printLine("Invalid choice")
		ticTacToe()
	
play()
	printTacToeToe()
	registerPlayerInput()
	nbTurn = nbTurn + 1
	updateState()
	if state == "":
		switchTurn()
		play()
	else:
		printResult()

switchTurn()
	if player == "X":
		player = "O"
	else:
		player = "X"

registerPlayerInput()
	print("Player " + player + " ->")
	String choice = readLine()
	// Use this condition because the priority of operation does not exist ...
	if choice + grid[0] == "11":
		grid[0] = player
	elseif choice + grid[1] == "22":
		grid[1] = player
	elseif choice + grid[2] == "33":
		grid[2] = player
	elseif choice + grid[3] == "44":
		grid[3] = player
	elseif choice + grid[4] == "55":
		grid[4] = player
	elseif choice + grid[5] == "66":
		grid[5] = player
	elseif choice + grid[6] == "77":
		grid[6] = player
	elseif choice + grid[7] == "88":
		grid[7] = player
	elseif choice + grid[8] == "99":
		grid[8] = player
	else:
		printLine("Invalid Input")
		registerPlayerInput()

updateState()
	checkWin()
	switchTurn()
	checkWin()
	switchTurn()
	if nbTurn + state == "9":
		state = "D" // DRAW 

checkWin()
	String value = player + player + player
	if grid[0] + grid[1] + grid[2] == value:
		state = player
	elseif grid[3] + grid[4] + grid[5] == value:
		state = player
	elseif grid[6] + grid[7] + grid[8] == value:
		state = player
	elseif grid[0] + grid[3] + grid[6] == value:
		state = player
	elseif grid[1] + grid[4] + grid[7] == value:
		state = player
	elseif grid[2] + grid[5] + grid[8] == value:
		state = player
	elseif grid[0] + grid[4] + grid[8] == value:
		state = player
	elseif grid[2] + grid[4] + grid[6] == value:
		state = player
	
menu()
	printLine("Welcome to the billy tic tac toe!")
	printLine("1 - Play")	
	printLine("2 - Exit")
	print("Votre choix ->")
	choice = readLine()

printTacToeToe()
	printLine("")
	printLine("  " + grid[0] + " | " + grid[1] + " | " + grid[2])
	printLine(" ___|___|___ " )
	printLine("  " + grid[3] + " | " + grid[4] + " | " + grid[5])
	printLine(" ___|___|___ " )
	printLine("  " + grid[6] + " | " + grid[7] + " | " + grid[8])
	printLine("    |   |  " )
	printLine("")
	
printResult()
	printTacToeToe()
	if state == "D":
		printLine("This is a DRAW!")
	else:
		printLine("The " + state +" win!")

reset()
	grid = {"1", "2", "3", "4", "5", "6", "7", "8", "9"}
	state = ""
	player = "X"
	nbTurn = 0
	